plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdk 31

    buildFeatures {
        compose = true
    }
    defaultConfig {
        minSdk 21
        targetSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerVersion = kotlinVersion
        kotlinCompilerExtensionVersion = composeVersion
    }
}

dependencies {
    debugApi project(":library")
    releaseApi "com.github.skgmn:cameraxx:0.2.0"

    implementation "androidx.compose.ui:ui:$composeVersion"
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url "https://maven.pkg.github.com/$GITHUB_ID/CameraXX"
                credentials {
                    username GITHUB_ID
                    password GITHUB_PACKAGES_TOKEN
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.skgmn'
                artifactId = 'cameraxx-compose'
                version = "0.2.0"
            }
        }
    }
}